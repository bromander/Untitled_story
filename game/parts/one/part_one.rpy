define ch_mir = False
define ch_con = False
define ch_content = False
define ch_tour = False

define notinoti = False #Было ли рассказано о возможности взаимодействовать с объектами на фоне

label start_part1:
    $ renpy.music.play("bg/Home.mp3", channel="music", loop=True, relative_volume=0.2, fadein=1)
    init python:
        config.layers.insert(0, "background")

    scene bg blackscreen onlayer background


    scene bg hall light_on
    show screen note("note1", 1400, 500)
"Спокойное тихое жужжание электроприборов звучит как занимательный выбор аккомпанемента к чуть менее монотонным новостям по сравнению с обычными. Новые ведущие? А впрочем, какая разница, лишь бы речь была разборчивой."
talking_head "...ожидается обильный снегопад, поэтому убедитесь, что не оставляете на улице транспортные средства, которые придётся откапывать!"

j "Ммм, какая забота..."
"Джек приподнимает брови в демонстративном, наполовину наигранном удивлении с ноткой иронии. Нечасто увидишь такое отступление от скрипта. Хоть что-то меняется."
"Бывает такое ощущение, когда все дни сливается воедино. Сложно понять, произошло некое событие вчера или неделю назад, а бытовые дела стали настолько рутинными, что порой задаёшься вопросом: я уже протирал пыль или ещё нет?"
"Иногда эта рутина настолько тебя затягивает, что даже перестаёшь хотеть что-либо делать, и охота просто лечь и уснуть."
"Но нельзя же спать всю жизнь? Надо её жить, проживать каждый момент, а не просто плыть по течению. Да и время подходящее - канун Нового года!"
"Зачем ждать нового года, следующего месяца, понедельника, красивого времеи на часах, когда можно начать что-то менять прямо сейчас, верно? Возможно, именно сегодня будет тот самый {i}день, когда всё изменится{/i}."
"Время сделать что-то небольшое, но подконтрольное и вернуть себе ощущение владения собственной жизнью!"

label to_do_list:
    hide screen note
    scene bg hall light_on with dissolve
    menu:
        "Что будем делать?"
        "почистить зеркало в ванной" if not ch_mir:
            $ ch_mir = True
            jump reflect_room
        "сходить в магазин за новым вкусом кофе":
            jump go_out_for
        "сходить в магазин за новым вкусом чая":
            jump go_out_for
        "запилить ролик" if not ch_con:
            "В каком месте это что-то небольшое, ты шо?! Это серьёзная работа!"
            $ ch_con = True
        "всё равно запилить ролик" if ch_con and not ch_content:
            "Ты пилишь контент прямо сейчас, что тебе ещё надо? Сходи, что ли, посмотри мир снаружи, бро..."
            $ ch_content = True
        "всё равно лечь спать" if not persistent.is_sleeped:
            jump tuk_tuk_tuk
        "проснуться" if persistent.is_sleeped:
            $ renpy.quit()
        "найти путёвку куда-нибудь на курорт" if not ch_tour:
            $ ch_tour = True
            jump scroll_tour
        "да не хочу я на улицу!" if ch_mir and ch_con and ch_content and ch_tour:
            "Верно. Мир снаружи страшное место. Там люди, которые смотрят. Там здания, которые дышат. Там тяжёлое небо давит на разум. Там двери, ведущие в никуда."
            "..."
            "...но этот монотонно жужжащий дом становится болотом. Его надо проветрить... и твой разум тоже."
            "Чем больше ты поддаёшься дремоте, тем больше ты умираешь... а в лесу хотят, чтобы ты жил."
            "..."
            "Потрогай траву. Потрогай снег. Потрогай небо."
            "Встреть следующий жизненный поворот лицом к лицу."
            "Двигайся вперёд. Навстречу миру. Навстречу сюжету. Навстречу жизни."
            menu:
                "двигаться":
                    jump go_out
                "замереть":
                    $ renpy.notify("Тёмная дрёма.")
                    scene bg blackscreen with fade
                    $ renpy.quit()
    jump to_do_list

label to_do_dum:
    "Тест окончен."
    $ renpy.quit()

screen mashenbka_text(_text):
    text _text:
        xpos 0.5 ypos 0.5    # позиция (0.5 = середина по оси)
        xanchor 0.5 yanchor 0.5   # анкор, чтобы центр текста совпадал с точкой
        color "#ff0000"      # цвет текста в HEX (красный)
        size 60
        slow_cps 30
        font "fonts/slifted.otf"


define af = renpy.audio.filter
label tuk_tuk_tuk: #wip
    show bg blackscreen with fade
    'А впрочем, зачем что-то менять? \nНе зря ведь говорят: пока что-то работает, не трогай. Правда, обычно это говорят о технике или программах, но почему бы не применить это к стилю жизни? '
    'В конце концов, стабильность - это признак мастерства!\n{w=0.5}...\n{w=0.5}...или стагнации.'
    'В любом случае, какой смысл париться о том, что тебе не вредит? Всё спокойно себе течёт своим ходом, никаких неожиданностей, никаких проблем, всё предсказуемо и мирно. \nДа и когда в последний раз сон вредил кому-либо?'
    show bg jopa_room light_on with fade
    show screen note("note4", 850, 700) with dissolve
    menu:
        'Вы точно уверены что хотите спать?'
        "Да":
            'Джек заходит в спальню и окидывает её усталым меланхоличным взглядом.'
            j 'Удивительно...  \n{w=0.2}Как можно так уставать, толком ничего серьёзного не сделав за весь день?'
            hide screen note with dissolve
            show bg jopa_room with dissolve
            'Лениво зевнув, он плюхнулся на кровать. {w=0.5}\nСчитать овец не пришлось - веки быстро потяжелели, мысли замедлились, а сознание подёрнулось тоненькой плёнкой дремоты и провалилось в сон.'
            $ renpy.music.stop(channel='music', fadeout=2.5)
            show bg blackscreen with slow_fade
            $ renpy.sound.play("sfx/door.mp3", channel="sound", loop=False, relative_volume=0.3)
            pause 5
            $ renpy.sound.stop(channel='sound')
            $ renpy.music.play("C.ogg", channel="music", loop=True, relative_volume=0.5, fadein=15)
            j 'Что?\n{w=0.3} Что-то разбудило меня. Кажется, где-то разбилось окно... или открылась дверь?'
            'Он оглядывается по сторонам и морщится.'
            $ renpy.sound.play("sfx/shelest_listev.ogg", channel="sound", loop=False, relative_volume=0.3)
            'Джек поднимается с кровати и тут же дёргается от хруста листвы под ногами. {w=0.5}\nОн хмурится, смотрит на пол и тут же с удивлением обнаруживает, что пол завален старой листвой и грязью. {w=0.3}\nОткуда здесь всё это?'
            $ renpy.sound.stop(channel='sound')
            'Всё ещё не до конца уверенный в своём пробуждении, Джек бросает взгляд на окно комнаты. {w=0.5}Оно закрыто. {w=0.5}Снаружи темно, и силуэты непохожи на привычный \"городской пейзаж\". Однако после попытки подойти поближе и посмотреть через стекло обнаруживается...'
            '...лес.'
            'Сумеречный осенний лес. \n{w=0.5}Тёмная дремучая глушь. \n{w=0.5}Голые деревья, сухая трава прямо под окном, как будто это первый этаж, летящие по ветру листья.'
            'Это не то место. Не место его дома.'
            j_scared 'Это какая-то странная шутка..? \nНо зачем- и кому это понадобилось?'
            'Тишина. \n{w=5.0}Только дом слегка поскрипывает от старости.'
            'Джек отпрянул от окна и провёл рукой по лицу с таким усилием, будто пытался его вытереть. {w=0.3}Обходя комки грязи и жухлые осенние листья самых разных деревьев, {w=0.3}разбросанные по полу, {w=0.3}он перемещается по всем остальным комнатам.'
            'Все окна закрыты и целы. \n{w=0.5}Все замки на местах. \n{w=0.5}Все двери в порядке.'
            'На пороге лежит записка, но она накарябана что называется \"как курица лапой\". {w=0.3}По обрывкам слов можно предположить, что это чей-то дневник, но мысль уловить сложно. \n{w=0.3}Бумага тоже сухая и пожелтевшая, как листья под ногами.'
            'Джек поправляет очки и потирает переносицу.'
            j_scared 'Да что за ерунда творится? Кто мог оставить весь этот бардак, есть здесь некому-'
            $ renpy.music.set_audio_filter("sound", af.Sequence(af.Reverb(0.5)))
            $ renpy.sound.play("sfx/plach.ogg", channel="sound", loop=False, relative_volume=0.3)
            pause 5
            $ renpy.music.set_audio_filter("sound", None)
            $ renpy.sound.stop(channel='sound')
            'Он осёкся на полуслове и замер, напрягая уши. \nГде-то в квартире послышались всхипывания и тоненькие звуки голоса. {w=0.3}Как будто ребёнок бормочет. {w=0.5}То ли плачет, {w=0.5}то ли смеётся, {w=0.5}по какой-то причине это очень трудно понять.'
            'Плачущие дети никогда не были добрым знаком. \nС другой стороны... хихиканье незнакомого чада тоже не выглядит позитивно.'
            'Несколько секунд Джек раздумывает. \n{w=0.5}Сбежать? \n{w=0.5}Вызвать полицию? \n{w=0.5}Атаковать неизвестное первым?'
            'Затем он поворачивается на звук и так же осторожно начинает двигаться на звук, бесшумно ступая из комнаты в комнату, держась вблизи стены и любых предметов, которые можно будет оперативно кинуть в угрозу. '
            'Вернувшись в спальню, в которой он проснулся, Джек замечает между кроватью и стенкой маленький невзрачный комок ткани, укрывающий чьё-то вздрагивающее сопящее тельце.'
            'Джек нерешительно делает шаг вперёд. \n{w=0.5}Потом ещё один. \n{w=0.5}И ещё один. \n{w=0.5}Незнакомое существо не пытается его атаковать'
            show leaf_girl
            j_scared 'Эм... эй? \nВсё, кхм, в порядке? \nТы кто и что здесь делаешь?'
            'Дитя затихает и осторожно оборачивается. \n{w=0.5}Это маленькая девочка. \n{w=0.5}Её волосы слегка лохматятся, а на лице следы лесной почвы. \n{w=0.5}Она широко открывает глаза и хватает Джека за руку.'
            hide leaf_girl
            mashenbka "\n\nЭто дурная дрёма... открой глаза, проснись!"
            nvl clear
            mashenbka "ПРОСНИСЬ"
            nvl clear
            show bg jopa_room with dissolve
            'Джек рывком садится на кровати, схватив ртом воздух так резко, как будто надолго задержал дыхание, и закашлялся. \n{w=0.5}Открыв глаза, он настороженно огляделся по сторонам. {w=0.5}Всё... как и прежде. На своих местах.'
            $ renpy.music.stop(channel='music', fadeout=10)
            'Это осознание позволяет выдохнуть с облегчением. Постепенно нормальное дыхание возвращается, а встревоженное сердце перестаёт колотиться и продолжает свою обычную ритмичную работу.'
            'Какое это всё-таки счастье - проснуться после тяжёлого сна!'
            j 'Похоже, пока хватит с меня спать...'
            show bg jopa_room light_on with dissolve
            $ renpy.music.play("bg/Home.mp3", channel="music", loop=True, relative_volume=0.2, fadein=1)
            'Что ж, видимо, надо заняться чем-то более активным. \n{w=0.5}Спать, возможно, и было замечательной опцией, но слишком хорошо тоже нехорошо.'
            'Быть может, это время сделать перерыв от всего этого застоя и немного размяться?'
            $ persistent.is_sleeped = True
            jump to_do_list

        "Нет":
            jump to_do_list
    scene bg jopa_room light_on

label scroll_tour:
    scene bg hall 2 light_on
    "Достав телефон, Джек открыл первый попавшийся сайт с турами и начал листать заголовки."
    show tour 1 at truecenter
    j "Это, конечно здорово, но роскошь будет дороговата..."
    show tour 2 at truecenter
    j "Тут билеты распроданы..."
    show tour 3 at truecenter
    j "Это вообще чёрт знает где..."
    show tour 4 at truecenter
    j "Это-"
    j "..."
    j "Зачем мне ехать в Арктику, если такой вкус можно спокойно позволить себе дома? Только морозиться почём зря."
    show tour 5 at truecenter
    j "О, я видел этого маскота где-то! Кажется... ммм... вроде это была та ерундовина с доставкой из передачи про разорившиеся компании, пренебрегавшие правами работников?"
    j "Она точно не стоит спонсирования. Вот если бы была возможность помочь пострадавшим работникам... впрочем, неважно, не моё дело."
    show tour 6 at truecenter
    j "ТААААК, окей, хорошо, хватит с меня на сегодня туров!"
    hide tour
    "Джек выключил экран телефона, откинулся на спинку дивана и тяжело вздохнул."
    jump to_do_list

screen show_random_mirror:
    add 'sprites/bg blackscreen.png'
    key "mouseup_1" action Hide("show_random_mirror")
    key "K_ESCAPE" action Hide("show_random_mirror")
    modal True
    $ _reflection = renpy.random.randint(1, 6)
    add f"sprites/scenes/jopa_home/reflections/reflect {_reflection}.png"

screen bathroom_mirror:
    imagebutton:
        xpos 1030
        ypos 80
        idle "sprites/scenes/jopa_home/mirror_handler.jpg"
        mouse 'wow'
        action Show("show_random_mirror")

label reflect_room:
    scene bg bathroom light_on
    "Где-то за стеной шумит вода, льющаяся по трубам. Воздух немного влажный, но в нём нет кислого запаха плесени. Стало быть, регулярная чистка даёт свои плоды."
    #{фон: непочищенное мутное зеркало над раковиной, но теперь рука открывает кран, бежит вода} Я ОТРИСУЮ
    $ renpy.sound.play("sfx/washing.mp3", channel="sound", loop=False, relative_volume=0.5)
    "Вода свежая, но не обжигающе холодная. Она контрастирует с тёплым воздухом в помещении и ослабляет сонливость. Тряпка для протирки стёкол быстро впитывает её и тихо поскрипывает, когда ею протирают зеркало. Или это скрип стекла?"
    $ renpy.sound.stop(channel='sound')
    $ renpy.sound.play("sfx/washing_pitch.ogg", channel="sound", loop=False, relative_volume=0.7)
    "Затем в ход идёт сухой край полотенца для рук. Скрип немного громче, а следом остаётся лишь кристально чистая поверхность."
    $ renpy.sound.stop(channel='sound')
    scene bg bathroom cleared light_on #временно?
    show screen bathroom_mirror
    #{фон: чистое зеркало над раковиной, блик закрывает отражение} Я ОТРИСУЮ
    #{Всплывающее уведомление Ren’Py: С некоторыми объектами можно взаимодействовать.}
    if not notinoti:
        $ renpy.notify("Попробуй заглянуть в зеркало ещё раз... или несколько.")
        $ notinoti = True
    #Сделать screen
    "Джек окидывает взглядом своё отражение."
    j "Ну, в целом даже ничего так!"
    "Получив удовлетворение от возвращения зеркалу качества Full HD, Джек покидает ванную комнату."
    hide screen bathroom_mirror
    jump to_do_list

label go_out_for:
    show screen note("note2", 450, 600)
    show bg kitchen light_on
    "Джек ещё раз бросает взгляд на одинокую пустую кружку на кухонном столе. Рыжеватая эмаль слабо поблёскивает в свете кухонной лампы. День ото дня в этой кружке заваривалось всё то же самое: одна и та же упаковка, один и тот же кипяток, одно и то же помешивание и пар от горячего напитка..."
    "Что ж, похоже, что пришло время внести немного новизны! Может, попробовать другого производителя, вкус или способ приготовления? Что ж, дальше будет виднее."
    label go_out:
        hide screen note
        scene bg exit light_on
        "Джек накинул на плечи пальто, быстрым движением вдел руки, поправил забившийся в рукаве пальто рукав рубашки и застегнулся."
        "Перед выходом он ещё раз окинул взглядом квартиру, убеждаясь, что не оставляет ничего лишнего во включённом состоянии, после чего натянул шапку и вышел за дверь. Щёлкнули ключи в замке, и прихожая затихла..."
        $ renpy.music.stop(channel='music', fadeout=5)
        $ renpy.sound.play("click.mp3")
        $ renpy.sound.play("door_sound.mp3")
        pause 5
        $ renpy.sound.play("down_stairs.mp3")
        jump start_part2


$ renpy.quit()
